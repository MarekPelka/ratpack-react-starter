buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:1.8.0"
    classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
  }
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "idea"
apply plugin: "com.github.johnrengelman.shadow"
apply from: 'versions.gradle'

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.hibernate:hibernate-core:$hibernateCoreVersion"
  compile "com.h2database:h2:$h2DbVersion"
  runtime "org.slf4j:slf4j-simple:1.7.30"
}

mainClassName = "pl.conexus.Main"

def frontedDir = "$projectDir/src/main/frontend/"

task appNpmInstall(type: Exec) {
  workingDir "$frontedDir"
  commandLine 'npm', 'install'
}

task appNpmBuild(type: Exec) {
  workingDir "$frontedDir"
  commandLine 'npm', 'run', 'build'
}

task copyFrontend(type: Copy) {
  from "$frontedDir/build"
  into "build/resources/main/static/."
}

// TODO: Perhaps this file can be created by gradle and use DEFAULT_BASE_DIR_MARKER_FILE_PATH for name.
task copyMarker(type: Copy) {
  from "$projectDir/.ratpack"
  into "build/resources/main/"
}

// TODO: Migrate npm tasks to NpmTask
// https://blog.indrek.io/articles/serving-react-apps-from-spring-boot/
//appNpmBuild.dependsOn appNpmInstall
copyFrontend.dependsOn appNpmBuild
shadowJar.dependsOn copyFrontend
shadowJar.dependsOn copyMarker